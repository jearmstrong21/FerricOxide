#include "std/math.fo"

struct v3f {
    f32 x;
    f32 y;
    f32 z;
}

mod v3f {
    v3f zero = v3f::from(0f);
    fn from(x: f32) -> v3f { return v3f { x: x, y: x, z: x }; }
    fn add(a: v3f, b: v3f) -> v3f { return v3f { x: a.x + b.x, y: a.y + b.y, z: a.z + b.z }; }
    fn sub(a: v3f, b: v3f) -> v3f { return v3f { x: a.x - b.x, y: a.y - b.y, z: a.z - b.z }; }
    fn mag(a: v3f) -> f32 { return math::sqrt(a.x * a.x + a.y * a.y + a.z * a.z); }
    fn mul(a: f32, b: v3f) -> v3f { return v3f { x: a * b.x, y: a * b.y, z: a * b.z }; }
    fn norm(a: v3f) -> v3f {
        f32 m = mag(a);
        return mul(1f / m, a);
    }
    fn dot(a: v3f, b: v3f) -> f32 { return a.x * b.x + a.y * b.y + a.z * b.z; }
    fn reflect(I: v3f, N: v3f) -> v3f {
        return norm(sub(
            I,
            mul(
                2f * dot(N, I) / dot(N, N),
                N
            )
        ));
    }
    fn cross(a: v3f, b: v3f) -> v3f {
        return v3f {
            x: a.y * b.z - a.z * b.y,
            y: a.z * b.x - a.x * b.z,
            z: a.x * b.y - a.y * b.x
        };
    }
    fn map(x: v3f, a1: v3f, b1: v3f, a2: v3f, b2: v3f) -> v3f {
        return v3f {
            x: math::map(x.x, a1.x, b1.x, a2.x, b2.x),
            y: math::map(x.y, a1.y, b1.y, a2.y, b2.y),
            z: math::map(x.z, a1.z, b1.z, a2.z, b2.z)
        };
    }
}